buildscript {
    ext {
        kotlinVersion = '1.4.21'
        springBootVersion = '2.4.1'
        mybatisGeneratorVersion = '1.4'
    }

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("gradle.plugin.com.arenagod.gradle:mybatis-generator-plugin:${mybatisGeneratorVersion}")
    }
}

allprojects {
    group = 'com.nexti4hack'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'eclipse'
    apply plugin: 'io.spring.dependency-management'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict", "-java-parameters"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict", "-java-parameters"]
            jvmTarget = "1.8"
        }
    }

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        implementation("org.springframework:spring-web")
        implementation('org.springframework.boot:spring-boot-starter-web')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
        implementation('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2')
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        implementation("org.jetbrains.kotlin:kotlin-reflect")

        runtimeOnly('mysql:mysql-connector-java')
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

}

project(':common') {
    apply plugin: "com.arenagod.gradle.MybatisGenerator"

    configurations {
        mybatisGenerator
    }
    // 「./gradlew mbGenerator」コマンドで作成
    mybatisGenerator {
        verbose = true
        configFile = "${rootDir}/db/mybatis/generatorConfig.xml"
    }
}

project(':connect-web') {
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':common')
        implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
        implementation('org.springframework.boot:spring-boot-starter-security')
        testImplementation('org.springframework.security:spring-security-test')
    }

    mainClassName = 'com.nexti4hack.webapp.WebappApplication'
    bootJar {
        baseName = 'nexti4hack-webapp'
        version = ''
        launchScript()
    }
}

project(':connect-api') {
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':common')
    }

    mainClassName = 'com.nexti4hack.api.ApiApplication'
    bootJar {
        baseName = 'nexti4hack-api'
        version = ''
        launchScript()
    }
}